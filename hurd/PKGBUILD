# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=hurd
pkgver=0.9.r107.ga8fecd7b
# branch 'master' in hurd/hurd.git
#_commit=679b58fae96597d6a41f314dfcb93aae2da7d554
_commit=a8fecd7be76342fea85b5129d450ab01738dcbdf
pkgrel=1
pkgdesc="The GNU Hurd"
arch=(i686)
url="http://www.gnu.org/software/hurd/hurd.html"
license=('GPL')
groups=('base')
# TODO: Make parted an optdepend (and makedepend probably)
depends=('gnumach' 'glibc' 'parted')  # initscripts
makedepends=('git' 'glibc')
options=('!makeflags')
backup=(etc/ttys)
source=(git+https://git.savannah.gnu.org/git/hurd/hurd.git#commit=$_commit
        ttys)
md5sums=('SKIP'
         '9d20e63e81835c76cc708ce6184f3530')

pkgver() {
  cd "$pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build

  cd $pkgname
  autoreconf -fi
}

build() {
  cd "$srcdir/build"

  # TODO: Remove this?
  export LDFLAGS="$(echo $LDFLAGS | sed 's/-Wl,--as-needed//')"
  
  ../$pkgname/configure \
     --prefix=/usr \
     --disable-profile \
     --enable-static-progs='iso9660fs,ext2fs,ufs'
  make
}

package() {
  cd "$srcdir/build"

  make prefix="$pkgdir/" install

  install -m 644 $srcdir/ttys $pkgdir/etc/ttys

  # system startup scripts are in the initscripts package
  rm $pkgdir/libexec/runsystem $pkgdir/libexec/rc

  # for rc.shutdown
  mv $pkgdir/sbin/halt $pkgdir/sbin/internal_halt
  mv $pkgdir/sbin/reboot $pkgdir/sbin/internal_reboot

  # TODO: Fix install prefix (maybe DESTDIR??)
  mkdir -p "$pkgdir"/usr/bin
  mv "$pkgdir"/include "$pkgdir"/usr/include
  mv "$pkgdir"/lib "$pkgdir"/usr/lib
  mv "$pkgdir"/sbin/* "$pkgdir"/usr/bin
  mv "$pkgdir"/bin/* "$pkgdir"/usr/bin
  rmdir "$pkgdir"/sbin "$pkgdir"/bin
  mv "$pkgdir"/share "$pkgdir"/usr/share
  mv "$pkgdir"/libexec/* "$pkgdir"/usr/lib
  rmdir "$pkgdir"/libexec

  # uptime gets provided by procps-ng
  mv "$pkgdir"/usr/bin/uptime{,-hurd}
  # fsck gets provided by util-linux
  mv "$pkgdir"/usr/bin/fsck{,-hurd}
}

