# $Id$
# Maintainer:
# Contributor: nut543 <kfs1@online.no>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=re2c
pkgver=1.0.3
pkgrel=1
arch=(x86_64)
depends=(gcc-libs)
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='http://re2c.org'
license=(GPL)
source=(re2c-$pkgver.tar.gz::https://github.com/skvadrik/re2c/archive/$pkgver.tar.gz
        autogen.patch)
sha1sums=('633d80252fefbb36901517b606f51b5baf3b14a7'
          '9fac985a54a5ad574b8e419c1bc8b34f97e775f1')

prepare() {
  cd $pkgname-$pkgver/re2c
  patch -Np1 -i "$srcdir"/autogen.patch
}

build() {
  cd $pkgname-$pkgver/re2c
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver/re2c
  make tests
}

package() {
  cd $pkgname-$pkgver/re2c
  make DESTDIR="$pkgdir" install
}
