# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

_name=gnumach
pkgname=gnumach-headers
pkgver=1.8.r40.gde813a9c
pkgrel=1
pkgdesc="GNU Mach"
arch=('i686')
license=('GPL')
url="https://www.gnu.org/software/hurd/microkernel/mach.html"
groups=('base-devel')
depends=("glibc")
makedepends=("git")
source=(git+https://git.savannah.gnu.org/git/hurd/gnumach.git)
sha512sums=('SKIP')

pkgver() {
  cd $_name
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  rm -rf build
  mkdir build

  cd $_name
  autoreconf -fi
}

build() {
  cd build

  ../$_name/configure \
    --prefix=/usr \
    --libexecdir=/usr/lib
}

package() {
  cd build
  make DESTDIR=$pkgdir install-data
}
